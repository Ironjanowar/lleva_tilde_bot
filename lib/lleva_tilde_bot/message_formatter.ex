defmodule LlevaTildeBot.MessageFormatter do
  alias LlevaTildeBot.Model.AnalyzedWord

  def help_command() do
    message = """
    EnvÃ­a una palabra con una tilde donde dudes que deberÃ­a ir.

    Por ejemplo:
      - _lÃ­nea_
      - _esdrÃºjula_
      - _monumÃ©nto_ (no lleva tilde)
      - _cÃ¡sa_ (no lleva tilde)
      - _clÃ¡ramente_ (no lleva tilde)

    El bot contestarÃ¡ con un anÃ¡lisis de la palabra.
    """

    {message, [parse_mode: "Markdown"]}
  end

  def about_command() do
    message = """
    _Este bot lo ha hecho_ [@Ironjanowar](https://github.com/Ironjanowar) _con amor â¤_

    Si quieres dar un poco de apoyo deja una estrella â­ en el [repositorio](https://github.com/Ironjanowar/lleva_tilde_bot)

    TambiÃ©n puedes usar la web https://llevatilde.es de la que se sacan los anÃ¡lisis. Este bot *no tiene relaciÃ³n con la web* solo extrae los anÃ¡lisis.
    """

    {message, [parse_mode: "Markdown"]}
  end

  def unknown_error() do
    message = "Ha ocurrido un error inesperado, lo siento :("
    {message, []}
  end

  def bad_input() do
    message = """
    Lo siento, solo puedo analizar palabras de una en una y con al menos una tilde donde creas que va la acentuaciÃ³n.

    Si necesitas ayuda manda el comando /help
    """

    {message, []}
  end

  def format_word_result(%AnalyzedWord{} = analyzed_word) do
    result = analyzed_word |> Map.from_struct() |> Enum.map(&format_result_field/1) |> Map.new()

    message =
      "ğŸ“œ *#{result.word}* ğŸ“œ\n"
      |> maybe_add(result.warning)
      |> maybe_add(result.syllables)
      |> maybe_add(result.analysis)
      |> maybe_add(result.conclusion)
      |> maybe_add(result.reason)
      |> maybe_add(result.result)
      |> maybe_add(result.diacritic_examples)

    {message, [parse_mode: "Markdown"]}
  end

  defp format_result_field({field, nil}), do: {field, nil}
  defp format_result_field({field, ""}), do: {field, nil}
  defp format_result_field({field, []}), do: {field, nil}
  defp format_result_field({:warning, warn}), do: {:warning, "âš  #{warn}\n"}
  defp format_result_field({:syllables, syl}), do: {:syllables, "*SÃ­labas*\n  __#{syl}__\n"}
  defp format_result_field({:analysis, analysis}), do: {:analysis, "#{analysis}\n"}
  defp format_result_field({:reason, reason}), do: {:reason, "__RazÃ³n:__ #{reason}\n"}
  defp format_result_field({:result, result}), do: {:result, "#{result}"}
  defp format_result_field({:conclusion, con}), do: {:conclusion, "*ConclusiÃ³n*\n#{con}\n"}

  defp format_result_field({:diacritic_examples, examples}),
    do: {:diacritic_examples, "*Se escribirÃ¡:*\n#{format_diacritic_examples(examples)}\n"}

  defp format_result_field(ignore), do: ignore

  defp maybe_add(text, []), do: text
  defp maybe_add(text, nil), do: text
  defp maybe_add(text, ""), do: text
  defp maybe_add(text, to_add), do: "#{text}\n#{to_add}"

  defp format_diacritic_examples(examples) do
    examples
    |> Enum.map(fn %{word: word, type: type, example: example} ->
      "\n  *#{word}* #{type}\n    _#{example}_"
    end)
    |> Enum.join("\n")
  end
end
